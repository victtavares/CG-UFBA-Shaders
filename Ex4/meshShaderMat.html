<html>
	
	<head>
		<title>MATA65 - Computacao Grafica</title>
		<h1>Algoritmos de Shading.</h1>
		<h3>Tipos de material na Three.JS: <i>meshShaderMaterial</i>.</h3>
	</head>
	
	<script id="gouraud-vs" type="x-shader/x-vertex">		
		 uniform vec3 uLPos; 
			
		 varying vec4 vColor; 
			
		 void main(void) { 
		 	vec4 lColor		= vec4(1.0, 1.0, 1.0, 1.0); 
		 	vec4 matAmb		= vec4(0.2, 0.2, 0.2, 1.0); 
		 	vec4 matDif 	= vec4(0.2, 1.0, 0.2, 1.0); 
		 	vec4 matSpec	= vec4(1.0, 1.0, 1.0, 1.0); 
			
		 	gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );  
			
		 	vec3 vPosW = (modelMatrix * vec4(position, 1.0)).xyz; 
			
		 	vec3 vNormal = normalize(normalMatrix * normal); 
			
		 	vec4 ambient = vec4(lColor.rgb * matAmb.rgb, matAmb.a); 
		
		 	vec3 vL = normalize(uLPos - vPosW); 
		 	float teta = max(dot(vL, vNormal), 0.0); 
					
		 	vec4 diffuse = vec4(lColor.rgb * matDif.rgb * teta, matDif.a); 
	
		 	vec3 vV = normalize(cameraPosition - vPosW); 
		 	vec3 vR = normalize(reflect(-vL, vNormal)); 
		 	float omega = max(dot(vV, vR), 0.0); 
		 	vec4 specular = vec4(lColor.rgb * matSpec.rgb * pow(omega,20.0), matSpec.a); 
			
		 	vColor = clamp(ambient + diffuse + specular, 0.0, 1.0); 
		 } 


	</script>
	
	<script id="gouraud-fs" type="x-shader/x-fragment">
		precision mediump float;
		
		varying vec4 vColor; 	
		
		void main(void) {
			gl_FragColor = vColor;
			} 	
	</script>

	<script id="phong-vs" type="x-shader/x-vertex">
		//https://github.com/mrdoob/three.js/issues/2613
		varying vec3 vNormal;
		//World position 
		varying vec3 vPosW;
		
		void main(void) { 		
			vPosW = (modelMatrix * vec4(position, 1.0)).xyz; 
			//https://github.com/mrdoob/three.js/issues/2613 - Normalize the normal matrix before pass to the shader
			vNormal = normalize(normalMatrix * normal); 			
			
			gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );  
			} 


	</script>
	
	<script id="phong-fs" type="x-shader/x-fragment">
		precision mediump float; 
		
		uniform vec3 uLPos; 
		
		varying vec3 vNormal;
		//World position 
		varying vec3 vPosW;
		
		void main(void) {
			//light color
		 	vec4 lColor		= vec4(1.0, 1.0, 1.0, 1.0);
		 	//ambient material
		 	vec4 matAmb		= vec4(0.2, 0.2, 0.2, 1.0); 
		 	//difuse material
		 	vec4 matDif 	= vec4(0.2, 1.0, 0.2, 1.0); 
		 	//Specular material
		 	vec4 matSpec	= vec4(1.0, 1.0, 1.0, 1.0); 
		 	
		 	//Ir(Ambiente) = I(global)*k(ambiente)
		 	vec4 ambient = vec4(lColor.rgb * matAmb.rgb, matAmb.a); 
			//light vector
		 	vec3 vL = normalize(uLPos - vPosW);
		 	//teta = Θ 
		 	float teta = max(dot(vL, vNormal), 0.0); 
			
			//Ir(difusa) = I(incidente)*k(difusa)*(Lvector.Nvector)
		 	vec4 diffuse = vec4(lColor.rgb * matDif.rgb * teta, matDif.a); 
			//Vector V
		 	vec3 vV = normalize(cameraPosition - vPosW);
		 	//Vector R - direction that a perfectly reflected ray of light would take from this point on the surface
		 	vec3 vR = normalize(reflect(-vL, vNormal)); 
		 	//omega = Ω
		 	float omega = max(dot(vV, vR), 0.0); 
		 	//Ir(especular) = I(incidente)*k(especular)*pow(Rvector.Vvector) 
		 	vec4 specular = vec4(lColor.rgb * matSpec.rgb * pow(omega,20.0), matSpec.a); 
			
		 	gl_FragColor = clamp(ambient + diffuse + specular, 0.0, 1.0); 
			} 	
	</script>

	<script type="text/javascript" src="../libs/three.js"></script>
    <script type="text/javascript" src="../libs/examples/js/controls/OrbitControls.js"></script>
    <script type="text/javascript" src="../libs/examples/js/loaders/OBJLoader.js"></script>
	<script type="text/javascript" src="meshShaderMat.js"></script>
	
	<body onload="init();">
		<div id="WebGL-output" ></div>
	</body>
</html>